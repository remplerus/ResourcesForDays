plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.57'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17) // Need this here so eclipse task generates correctly.

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

def resourceTargets = ['META-INF/mods.toml', 'pack.mcmeta']
def replaceProperties = [
        minecraft_version   : mc_version, minecraft_version_range: minecraft_version_range,
        neo_version         : neo_version, neo_version_range: neo_version_range,
        loader_version_range: loader_version_range,
        mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
        mod_authors         : mod_authors, mod_description: mod_description,
        exnihilo_range      : exnihilo_range, exnihilo_version: exnihilo_version
]
tasks.withType(ProcessResources).configureEach {
    inputs.properties replaceProperties

    filesMatching(resourceTargets) {
        expand replaceProperties + [project: project]
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name 'jei'
        url 'https://maven.blamejared.com'
        content {
            includeGroup 'mezz.jei'
        }
    }
    maven {
        name 'REI'
        url "https://maven.shedaniel.me"
        content {
            includeGroup 'me.shedaniel'
            includeGroup 'me.shedaniel.cloth'
            includeGroup 'dev.architectury'
        }
    }

    maven {
        name 'NovaMachina Mods - Release'
        url 'https://dl.cloudsmith.io/public/novamachina-mods/release/maven/'
        content {
            includeGroup 'novamachina.novacore'
            includeGroup 'novamachina.exnihilosequentia'
            includeGroup 'novamachina.exnihilothermal'
            includeGroup 'novamachina.exnihilomekanism'
            includeGroup 'novamachina.exnihiloae'
            includeGroup 'novamachina.exnihilotinkers'
        }
        mavenContent {
            releasesOnly()
        }
    }
    maven {
        name 'NovaMachina Mods - Snapshot'
        url 'https://dl.cloudsmith.io/public/novamachina-mods/snapshot/maven/'
        content {
            includeGroup 'novamachina.novacore'
            includeGroup 'novamachina.exnihilosequentia'
            includeGroup 'novamachina.exnihilothermal'
            includeGroup 'novamachina.exnihilomekanism'
            includeGroup 'novamachina.exnihiloae'
            includeGroup 'novamachina.exnihilotinkers'
        }
        mavenContent {
            snapshotsOnly()
        }
    }

    maven {
        name 'cursemaven'
        url 'https://cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"

    //implementation fg.deobf("novamachina.exnihilosequentia:exnihilosequentia:${minecraft_version}-${exnihilo_version}")

    //implementation fg.deobf("novamachina.novacore:novacore:${minecraft_version}-${nova_version}")
    runtimeOnly "me.shedaniel:RoughlyEnoughItems-neoforge:14.+"
    runtimeOnly "me.shedaniel.cloth:cloth-config-neoforge:13.+"
    runtimeOnly "dev.architectury:architectury-neoforge:11.+"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}